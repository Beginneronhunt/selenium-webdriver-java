apply plugin: "java"

jar {
    archiveBaseName = "selenium-webdriver-junit4"
    archiveVersion = "1.0.0-SNAPSHOT"
}

compileTestJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.compilerArgs += "-parameters"
}

test {
    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }

    systemProperties System.properties

    if (project.hasProperty("excludeTests")) {
        exclude project.property("excludeTests")
    }

    ext.failedTests = []

    tasks.withType(Test) {
        afterTest { TestDescriptor descriptor, TestResult result ->
            if(result.resultType == org.gradle.api.tasks.testing.TestResult.ResultType.FAILURE) {
                failedTests << ["${descriptor.className}::${descriptor.name}"]
            }
        }
    }

    gradle.buildFinished {
        if(!failedTests.empty){
            println "Failed test(s) for ${project.name}:"
            failedTests.each { failedTest ->
                println failedTest
            }
        }
    }
}

repositories {
    mavenCentral()

    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

ext {
    slf4jVersion  = "1.7.32"
    logbackVersion  = "1.2.6"

    seleniumVersion  = "4.0.0"
    junit4Version = "4.13.2"
    assertjVersion  = "3.21.0"
    wdmVersion = "5.0.4-SNAPSHOT"
}

dependencies {
    implementation("org.slf4j:slf4j-api:${slf4jVersion}")
    implementation("ch.qos.logback:logback-classic:${logbackVersion}")

    testImplementation("org.seleniumhq.selenium:selenium-java:${seleniumVersion}")
    testImplementation("junit:junit:${junit4Version}")
    testImplementation("org.assertj:assertj-core:${assertjVersion}")
    testImplementation("io.github.bonigarcia:webdrivermanager:${wdmVersion}")

    testImplementation("org.seleniumhq.selenium:selenium-grid:${seleniumVersion}")
}
