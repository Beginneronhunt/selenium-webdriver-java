apply plugin: "java"

jar {
    archiveBaseName = "selenium-webdriver-junit5-seljup"
    archiveVersion = "1.0.0-SNAPSHOT"
}

compileTestJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.compilerArgs += "-parameters"
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }

    systemProperties System.properties

    ext.failedTests = []

    tasks.withType(Test) {
        afterTest { TestDescriptor descriptor, TestResult result ->
            if(result.resultType == org.gradle.api.tasks.testing.TestResult.ResultType.FAILURE) {
                failedTests << ["${descriptor.className}::${descriptor.name}"]
            }
        }
    }

    gradle.buildFinished {
        if(!failedTests.empty){
            println "Failed test(s) for ${project.name}:"
            failedTests.each { failedTest ->
                println failedTest
            }
        }
    }
}

repositories {
    mavenCentral()
}

ext {
    slf4jVersion  = "1.7.32"
    logbackVersion  = "1.2.5"

    seleniumVersion  = "4.0.0-beta-4"
    junit5Version = "5.7.2"
    selJupVersion = "4.0.1"
    assertjVersion  = "3.20.2"
}


dependencies {
    implementation("org.slf4j:slf4j-api:${slf4jVersion}")
    implementation("ch.qos.logback:logback-classic:${logbackVersion}")

    testImplementation("org.seleniumhq.selenium:selenium-java:${seleniumVersion}")
    testImplementation("org.junit.jupiter:junit-jupiter:${junit5Version}")
    testImplementation("io.github.bonigarcia:selenium-jupiter:${selJupVersion}")
    testImplementation("org.assertj:assertj-core:${assertjVersion}")
}
